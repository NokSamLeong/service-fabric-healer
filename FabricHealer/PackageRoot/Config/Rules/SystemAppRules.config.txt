## Logic rules for Service Fabric System Service repairs.

## First, check if we are inside the run interval. If inside run interval, then cut (no other rules will be processed).
## Note: FO only generates Application (System) level warnings for system services. There will only ever be ApplicationName as "fabric:/System" in the FO health data that FH emits, so this is an optional argument.
## Mitigate(AppName="fabric:/System") :- CheckInsideRunInterval(RunInterval=01:00:00), !.

## TimeScopedRestartFabricNode is an internal predicate to check for the number of times a system service node restart repair has run to completion within a supplied time window. 
## If Completed Repair count is less then supplied value, then run RestartFabricNode mitigation.
TimeScopedRestartFabricNode(?count, ?time) :- GetRepairHistory(?repairCount, TimeWindow=?time), ?repairCount < ?count, 
	RestartFabricNode().

## TimeScopedRestartFabricSystemProcess is an internal predicate to check for the number of times a System service process restart repair has run to completion within a supplied time window. 
## If Completed Repair count is less then supplied value, then run RestartFabricSystemProcess mitigation.
TimeScopedRestartFabricSystemProcess(?count, ?time) :- GetRepairHistory(?repairCount, TimeWindow=?time), ?repairCount < ?count, 
	RestartFabricSystemProcess().
	
## Mitigation queries for multiple metrics and targets.

## Restarting a Fabric Node is an expensive operation. However, this is done in a data-safe way by FabricHealer. Fabric or FabricHost processes are not just killed.
## You probably only need to do this when there is no other viable option. Generally, you can just restart a system service process (not Fabric or FabricHost). If a node needs to be restarted
## it will be done by an instance of FH not running on the same node (for hopefully obvious reasons).

## CPU Time - Percent
Mitigate(AppName="fabric:/System", MetricName="CpuPercent", MetricValue=?MetricValue) :- ?MetricValue >= 80,
	TimeScopedRestartFabricNode(4, 08:00:00).

## Memory Use - Megabytes in use
Mitigate(AppName="fabric:/System", MetricName="MemoryMB", MetricValue=?MetricValue) :- ?MetricValue >= 2048,
	TimeScopedRestartFabricNode(4, 08:00:00).

## Memory Use - Percent in use
Mitigate(AppName="fabric:/System", MetricName="MemoryPercent", MetricValue=?MetricValue) :- ?MetricValue >= 40,
	TimeScopedRestartFabricNode(4, 08:00:00).

## Ephemeral Ports in Use
Mitigate(AppName="fabric:/System", MetricName="EphemeralPorts", MetricValue=?MetricValue) :- ?MetricValue >= 800,
	TimeScopedRestartFabricNode(4, 08:00:00).

## Open File Handles - Specific system service process on either Windows or Linux.
## Note the use of the Contains system predicate here, as Windows and Linux have different process names for some components,
## that is, different file extensions (.dll, .exe or none, for Linux, for example, and *always* none for Windows (because FO emits just the name, not including the extension, for Windows procs)).
## Restart the offending Fabric system process named FabricGateway, regardless of OS.
Mitigate(AppName="fabric:/System", MetricName="FileHandles", SystemServiceProcessName=?SysProcName) :- Contains("FabricGateway", ?SysProcName),
	RestartFabricSystemProcess().

## Open File Handles - Linux-only: Any SF system service besides Fabric or FabricHost.
## Restart the offending Fabric system process.
Mitigate(AppName="fabric:/System", MetricName="FileHandles", OS="Linux", SystemServiceProcessName=?SysProcName) :- not(?SysProcName == "Fabric" || ?SysProcName == "FabricHost"), 
	RestartFabricSystemProcess().

## Open File Handles - Linux, Fabric or FabricHost. In these cases, we want a safe (graceful) restart of the Fabric node; not just kill the process, which will restart the node, but not gracefully.
## Restart the Fabric node where the offending instance is running.
Mitigate(AppName="fabric:/System", MetricName="FileHandles", OS="Linux", SystemServiceProcessName="Fabric") :- TimeScopedRestartFabricNode(2, 08:00:00).
Mitigate(AppName="fabric:/System", MetricName="FileHandles", OS="Linux", SystemServiceProcessName="FabricHost") :- TimeScopedRestartFabricNode(2, 08:00:00).